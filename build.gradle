import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
  id 'java-library'
  id 'maven-publish'
}

group 'legend'
version '0.1'

sourceCompatibility = targetCompatibility = 1.17

project.ext.lwjglVersion = "3.2.3"
project.ext.jomlVersion = "1.10.1"

if(DefaultNativePlatform.getCurrentOperatingSystem().isWindows()) {
  project.ext.lwjglNatives = "natives-windows"
}

if(DefaultNativePlatform.getCurrentOperatingSystem().isLinux()) {
  project.ext.lwjglNatives = "natives-linux"
}

if(DefaultNativePlatform.getCurrentOperatingSystem().isMacOsX()) {
  project.ext.lwjglNatives = "natives-macos"
}

java {
  withJavadocJar()
  withSourcesJar()
}

repositories {
  mavenCentral()
}

dependencies {
  api group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
  api group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
  api group: 'org.fusesource.jansi', name: 'jansi', version: '2.3.2'

  api platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

  api "org.lwjgl:lwjgl"
  api "org.lwjgl:lwjgl-glfw"
  api "org.lwjgl:lwjgl-nuklear"
  api "org.lwjgl:lwjgl-opengl"
  api "org.lwjgl:lwjgl-stb"
  runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-nuklear::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
  api "org.joml:joml:${jomlVersion}"

  api group: 'it.unimi.dsi', name: 'fastutil', version: '8.4.0'

  api group: 'net.oneandone.reflections8', name: 'reflections8', version: '0.11.5'

  api group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'lod-core'
      from components.java
      pom {
        name = 'LoD Core'
      }
    }
  }
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
